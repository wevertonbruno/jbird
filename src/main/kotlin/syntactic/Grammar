expression -> literal | unary_expression | binary_expression | grouping;

literal -> NUMBER | STRING | TRUE | FALSE | NIL;

binary_expression -> expression operator expression;

grouping -> L_PAREN expression R_PAREN;

=======

program -> declaration* EOF;

declaration -> {NL} ( var_declaration | func_declaration | statement );

statement -> {NL} (expr_stmt | print_stmt | block_stmt | if_statement | while_statement | do_while_stmt);

do_while_stmt -> DO block_stmt WHILE L_PAREN expression R_PAREN separator;

while_statement -> WHILE L_PAREN expression R_PAREN statement;

if_statement -> IF L_PAREN expression R_PAREN statement ( ELSE statement )?;

block_stmt -> L_BRACE {NL} declaration* {NL} R_BRACE;

expr_stmt -> conditional_expression separator;

print_stmt -> PRINT expression separator;

var_declaration -> VAR IDENTIFIER (EQUAL expression)? separator;

func_declaration -> FUNC function;

function -> IDENTIFIER L_PAREN parameters? R_PAREN block_stmt;

parameters -> IDENTIFIER (COMMA IDENTIFIER)*;

separator -> SEMICOLON | BREAK_LINE;

conditional_expression -> expression | expression ? expression : conditional_expression;

expression -> assigment;

assigment -> IDENTIFIER EQUAL assigment | logical_or;

logical_or -> logical_and (OR logical_and)*;

logical_and -> equality (AND equality)*;

equality -> comparison ( ( EQUAL_EQUAL | NOT_EQUAL ) comparison ) *;

comparison -> term ( ( GREATER | GREATER_THEN | LESS | LESS_THEN ) term )*;

term -> factor ( ( PLUS | MINUS ) factor )*;

factor -> unary ( (MULTI | SLASH) unary ) *;

unary -> (EXCLAMATION | MINUS) unary | call;

call -> primary ( L_PAREN arguments? R_PAREN )*;

primary -> literal | grouping | IDENTIFIER;

NL -> NEW_LINE;

arguments -> expression ( COMMA expression )*;

======

ternary_expression -> expression QUESTION_MARK expression COLON expression;
